##   -*- autoconf -*-

AC_DEFUN(HLM_CHECK_FFTW,
[
AC_MSG_CHECKING([for fftw])

if test -z "$LIB_FFTW"; then
  LIB_FFTW="-lfftw"
fi

fftw_libraries=""
fftw_includes=""
ac_fftw_includes=NO
ac_fftw_libraries=NO

AC_ARG_WITH(fftw-dir,
    [  --with-fftw-dir=DIR       where the root of fftw is installed ],
    [  ac_fftw_includes="$withval"/include
       ac_fftw_libraries="$withval"/lib
    ])

AC_ARG_WITH(fftw-includes,
    [  --with-fftw-includes=DIR  where the fftw includes are. ],
    [  ac_fftw_includes="$withval"
    ])

AC_ARG_WITH(fftw-libraries,
    [  --with-fftw-libraries=DIR where the fftw library is installed.],
    [  ac_fftw_libraries="$withval"
    ])

AC_CACHE_VAL(ac_cv_have_fftw,
[
# find fftw lib
fftw_libdirs="$ac_fftw_libraries /usr/local/lib/fftw /usr/local/lib /usr/lib/fftw /usr/lib /software/fftw-2.1.3/SunOS-5.6/lib /home/meine/fftw/lib"
if test "$ac_fftw_libraries" != "NO"; then
  fftw_libdir=$ac_fftw_libraries
else
  fftw_libdirs="$ac_fftw_libraries $fftw_libdirs"
  fftw_libdir=NONE
  for dir in $fftw_libdirs; do
    try="ls -1 $dir/libfftw.*"
    if test -n "`$try 2> /dev/null`"; then
	fftw_libdir=$dir
	break
    else
	echo "tried $try unsuccessfully" >&AC_FD_CC
    fi
  done
fi
ac_fftw_libraries="$fftw_libdir"
echo "trying fftw libraries from $ac_fftw_libraries" >&AC_FD_CC

# find fftw includes
fftw_incdirs="$ac_fftw_includes /usr/local/include/fftw /usr/local/include /usr/include/fftw /usr/include"
AC_FIND_FILE(fftw.h, $fftw_incdirs, fftw_incdir)
ac_fftw_includes="$fftw_incdir"
echo "trying fftw includes from $ac_fftw_includes" >&AC_FD_CC

# try to compile test prog
AC_LANG_SAVE
AC_LANG_C

ac_cflags_safe="$CFLAGS"
ac_ldflags_safe="$LDFLAGS"
ac_libs_safe="$LIBS"

CFLAGS="$CFLAGS -I$fftw_incdir"
LDFLAGS="$LDFLAGS -L$fftw_libdir"
LIBS="$LIBS $LIB_FFTW -lm"

HLM_FFTW_TESTPROGRAM

if AC_TRY_EVAL(ac_link) && test -s conftest; then
  rm -f conftest*
else
  echo "configure: failed program was:" >&AC_FD_CC
  cat conftest.$ac_ext >&AC_FD_CC
  ac_fftw_libraries="NO"
fi
rm -f conftest*
CXXFLAGS="$ac_cxxflags_safe"
LDFLAGS="$ac_ldflags_safe"
LIBS="$ac_libs_safe"

AC_LANG_RESTORE

# done, save/cache results
if test "$ac_fftw_includes" = NO || test "$ac_fftw_libraries" = NO; then
  ac_cv_have_fftw="have_fftw=no"
  ac_fftw_notfound=""
  if test "$ac_fftw_includes" = NO; then
    if test "$ac_fftw_libraries" = NO; then
      ac_fftw_notfound="(headers and libraries)";
    else
      ac_fftw_notfound="(headers)";
    fi
  else
    ac_fftw_notfound="(libraries)";
  fi

  AC_MSG_ERROR([fftw $ac_fftw_notfound not found. Please check your installation!
For more details about this problem, look at the end of config.log.])
else
  have_fftw="yes"
fi
])

eval "$ac_cv_have_fftw"

if test "$have_fftw" != yes; then
  AC_MSG_RESULT([$have_fftw]);
else
  ac_cv_have_fftw="have_fftw=yes \
    ac_fftw_includes=$ac_fftw_includes ac_fftw_libraries=$ac_fftw_libraries"
  AC_MSG_RESULT([libraries $ac_fftw_libraries, headers $ac_fftw_includes $USING_FFTW_MT])

  fftw_includes="$ac_fftw_includes"
  fftw_libraries="$ac_fftw_libraries"
fi

AC_SUBST(fftw_includes)
AC_SUBST(fftw_libraries)

# paths known, now create _LDFLAGS/_INCLUDES

if test -z "$fftw_includes"; then
 FFTW_INCLUDES="";
else
 FFTW_INCLUDES="-I$fftw_includes"
 all_includes="$FFTW_INCLUDES $all_includes"
fi

if test -z "$fftw_libraries"; then
 FFTW_LDFLAGS=""
else
 FFTW_LDFLAGS="-L$fftw_libraries --rpath$fftw_libraries"
fi

AC_SUBST(FFTW_INCLUDES)
AC_SUBST(FFTW_LDFLAGS)
AC_SUBST(LIB_FFTW)
])

AC_DEFUN(HLM_FFTW_TESTPROGRAM,
[cat > conftest.$ac_ext <<EOF
#include "fftw.h"

int main() {
    fftwnd_plan fft_plan=
	fftw2d_create_plan(10, 10, FFTW_FORWARD, FFTW_ESTIMATE | FFTW_IN_PLACE);
    fftwnd_destroy_plan(fft_plan);
    return 0;
}
EOF
])

