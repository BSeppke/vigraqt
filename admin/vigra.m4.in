##   -*- autoconf -*-

AC_DEFUN(HLM_CHECK_VIGRA,
[
if test -z "$LIB_VIGRAIMPEX"; then
  LIB_VIGRAIMPEX="-lvigraimpex -ltiff -ljpeg"
fi

AC_MSG_CHECKING([for vigra])
vigra_libraries=""
vigra_includes=""

ac_vigra_includes=NO ac_vigra_libraries=NO ac_vigra_bindir=NO
AC_ARG_WITH(vigra-dir,
    [  --with-vigra-dir=DIR       where the root of vigra is installed ],
    [  ac_vigra_includes="$withval"/include
       ac_vigra_libraries="$withval"/lib
       ac_vigra_bindir="$withval"/bin
    ])

AC_ARG_WITH(vigra-includes,
    [  --with-vigra-includes=DIR  where the vigra includes are. ],
    [  ac_vigra_includes="$withval"
    ])

AC_ARG_WITH(vigra-libraries,
    [  --with-vigra-libraries=DIR where the vigra library is installed.],
    [  ac_vigra_libraries="$withval"
    ])

AC_CACHE_VAL(ac_cv_have_vigra,
[
# find vigra lib
if test ! "$ac_vigra_libraries" = "NO"; then
  vigra_libdir=$ac_vigra_libraries
else
  vigra_libdirs="$ac_vigra_libraries /usr/local/lib/vigra /usr/local/lib /usr/lib/vigra /usr/lib"
  vigra_libdir=NONE
  for dir in $vigra_libdirs; do
    try="ls -1 $dir/libvigraimpex.*"
    if test -n "`$try 2> /dev/null`"; then
	vigra_libdir=$dir
	break
    else
	echo "no libs in $dir" >&AC_FD_CC
    fi
  done
fi
ac_vigra_libraries="$vigra_libdir"
echo "found libs: ac_vigra_libraries=$ac_vigra_libraries" >&AC_FD_CC

# find vigra includes
vigra_incdirs="$ac_vigra_includes /usr/local/include /usr/include"
AC_FIND_FILE(vigra/impex.hxx, $vigra_incdirs, vigra_incdir)
ac_vigra_includes="$vigra_incdir"
echo "found includes: ac_vigra_includes=$ac_vigra_includes" >&AC_FD_CC

# try to compile test prog
AC_LANG_SAVE
AC_LANG_CPLUSPLUS

ac_cxxflags_safe="$CXXFLAGS"
ac_ldflags_safe="$LDFLAGS"
ac_libs_safe="$LIBS"

CXXFLAGS="$CXXFLAGS -I$vigra_incdir"
LDFLAGS="$LDFLAGS -L$vigra_libdir"
LIBS="$LIBS $LIB_VIGRAIMPEX"

HLM_VIGRA_TESTPROGRAM

if AC_TRY_EVAL(ac_link) && test -s conftest; then
  rm -f conftest*
else
  echo "configure: failed program was:" >&AC_FD_CC
  cat conftest.$ac_ext >&AC_FD_CC
  ac_vigra_libraries="NO"
fi
rm -f conftest*
CXXFLAGS="$ac_cxxflags_safe"
LDFLAGS="$ac_ldflags_safe"
LIBS="$ac_libs_safe"

AC_LANG_RESTORE

# done, save/cache results
if test "$ac_vigra_includes" = NO || test "$ac_vigra_libraries" = NO; then
  ac_cv_have_vigra="have_vigra=no"
  ac_vigra_notfound=""
  if test "$ac_vigra_includes" = NO; then
    if test "$ac_vigra_libraries" = NO; then
      ac_vigra_notfound="(headers and libraries)";
    else
      ac_vigra_notfound="(headers)";
    fi
  else
    ac_vigra_notfound="(libraries)";
  fi

  AC_MSG_ERROR([vigra $ac_vigra_notfound not found. Please check your installation!
For more details about this problem, look at the end of config.log.])
else
  have_vigra="yes"
fi
])

eval "$ac_cv_have_vigra"

if test "$have_vigra" != yes; then
  AC_MSG_RESULT([$have_vigra]);
else
  ac_cv_have_vigra="have_vigra=yes \
    ac_vigra_includes=$ac_vigra_includes ac_vigra_libraries=$ac_vigra_libraries"
  AC_MSG_RESULT([libraries $ac_vigra_libraries, headers $ac_vigra_includes $USING_VIGRA_MT])

  vigra_includes="$ac_vigra_includes"
  vigra_libraries="$ac_vigra_libraries"
fi

AC_SUBST(vigra_includes)
AC_SUBST(vigra_libraries)

# paths known, now create _LDFLAGS/_INCLUDES

if test -z "$vigra_includes"; then
 VIGRA_INCLUDES="";
else
 VIGRA_INCLUDES="-I$vigra_includes"
 all_includes="$VIGRA_INCLUDES $all_includes"
fi

if test -z "$vigra_libraries"; then
 VIGRA_LDFLAGS=""
else
 VIGRA_LDFLAGS="-L$vigra_libraries"
fi

AC_SUBST(VIGRA_INCLUDES)
AC_SUBST(VIGRA_LDFLAGS)
AC_SUBST(LIB_VIGRAIMPEX)
])

AC_DEFUN(HLM_VIGRA_TESTPROGRAM,
[cat > conftest.$ac_ext <<EOF
#include "vigra/impex.hxx"

int main() {
    vigra::ImageImportInfo info("test.jpg");
}
EOF
])

